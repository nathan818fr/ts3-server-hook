function(find_sources_internal VAR DIRS PLATFORMS EXTS)
  set(_ret "")
  foreach (_dir IN LISTS DIRS)
    foreach (_ext IN LISTS EXTS)
      file(GLOB_RECURSE _files RELATIVE "${_dir}" "${_dir}/${_ext}")
      list(FILTER _files EXCLUDE REGEX "^platform[\\/].*")
      foreach (_platform IN LISTS PLATFORMS)
        file(GLOB_RECURSE _platform_files RELATIVE "${_dir}" "${_dir}/platform/${_platform}/${_ext}")
        list(APPEND _files "${_platform_files}")
      endforeach ()
      list(TRANSFORM _files PREPEND "${_dir}/")
      list(APPEND _ret "${_files}")
    endforeach ()
  endforeach ()
  set("${VAR}" "${_ret}" PARENT_SCOPE)
endfunction()

function(find_sources NAME DIRS PLATFORMS)
  find_sources_internal(_sources "${DIRS}" "${PLATFORMS}" "*.c;*.cc")
  find_sources_internal(_headers "${DIRS}" "${PLATFORMS}" "*.h;*.hh")
  set("${NAME}_SOURCES" "${_sources}" PARENT_SCOPE)
  set("${NAME}_HEADERS" "${_headers}" PARENT_SCOPE)
endfunction()
